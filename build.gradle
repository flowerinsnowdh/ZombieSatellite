buildscript {
    repositories {
        maven {
            url 'https://maven.aliyun.com/repository/public'
        }
        maven {
            name = "forge"
            url = "https://files.minecraftforge.net/maven"
        }
        maven {
            url 'https://repo.spongepowered.org/repository/maven-public/'
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT'
        classpath 'org.spongepowered:mixingradle:0.6-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'org.spongepowered.mixin'

version = "1.1.1"
group= "online.flowerinsnow"
archivesBaseName = "zombiesatellite"

minecraft {
    version = "1.8.9-11.15.1.2318-1.8.9"
    runDir = "run"

    mappings = "stable_20"
    makeObfSourceJar = false
}

configurations {
    embed
    compile.extendsFrom(embed)
}

repositories {
    maven {
        url 'https://repo.spongepowered.org/repository/maven-public/'
    }
}

dependencies {
    embed('org.spongepowered:mixin:0.7.11-SNAPSHOT') {
        exclude module: 'guava'
        exclude module: 'commons-io'
        exclude module: 'gson'
    }
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

tasks.withType(JavaCompile).configureEach {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    options.encoding = 'UTF-8'
}

jar {
    from configurations.embed.asFileTree.files.collect {
        zipTree(it)
    }

    manifest.attributes([
            "TweakClass": "online.flowerinsnow.zombiessatellite.tweaker.ZombiesSatelliteTweaker"
    ])
    /*
    manifest.attributes([
            "FMLCorePlugin": "online.flowerinsnow.zombiessatellite.hook.ZombieSatellitePlugin",
            "FMLCorePluginContainsFMLMod": true
    ])
     */
}

mixin {
    add sourceSets.main, "mixins.zombiessatellite.refmap.json"
}
